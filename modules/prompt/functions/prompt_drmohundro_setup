#
# Basic theme like terminalparty in oh-my-zsh... isn't quite the same, but close
#
# Authors:
#   David Mohundro <david@mohundro.com>
#

# Load dependencies.
pmodload 'helper'

function prompt_drmohundro_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_drmohundro_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_drmohundro_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:dirty' format '%%B%F{white}*%s%f%%b'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '(%F{blue}%f$(coalesce "%b" "%p" "%c")%s%D)' \
    'rprompt' '%A%B'

  # Define prompts.
  PROMPT='%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='%F{cyan}%~%f${editor_info[overwrite]}%(?:: %F{red}⏎%f)${git_info[rprompt]} ${git_info:+${(e)git_info[prompt]}}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_drmohundro_setup "$@"
